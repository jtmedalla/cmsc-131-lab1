;
; decode.asm - extract every field from a 20-byte IPv4 header.
;
; This is your starting point. It assembles and links as-is, so the build
; works before you write any code. Right now it stores nothing, so renpkt
; prints the zeros driver.c put in the struct. Your job is to replace that
; with the extraction described below.
;
; The contract, from driver.c:
;
;       struct ipv4_fields *out   [ebp+12]
;       unsigned char *hdr        [ebp+8]
;
; hdr points at twenty bytes in network byte order. out points at the struct
; documented in driver.c. Its offsets are:
;
;   +0 version   +4 ihl    +8 dscp   +12 ecn   +16 total_length
;   +20 identification    +24 flags  +28 fragment_offset
;   +32 ttl      +36 protocol       +40 checksum
;   +44 src[0..3]                   +48 dst[0..3]
;
; Every int member is 4 bytes, so a plain 32-bit store fills one. The
; addresses are four single-byte stores each.
;
; Do not clobber ebx, esi, edi, or ebp. C assumes they survive your call.
; Return in eax (driver.c ignores it here, so returning 0 is fine).
;

; Windows C puts a leading underscore on every exported name. Linux C does
; not. The Makefile passes -d ELF_TYPE on Linux. This block then respells
; the names below to match. asm_io.inc does the same for _asm_main in the
; bootcamp blocks. Leave this block alone.
%ifdef ELF_TYPE
  %define _decode_header decode_header
  section .note.GNU-stack noalloc noexec nowrite progbits
%endif

segment .text
        global  _decode_header
_decode_header:
        enter   0,0
        pusha

        ;
        ; TODO: read the header and fill the struct.
        ;
        ; The field-by-field layout is the table in the manual. The notes
        ; that matter before you start:
        ;
        ;   * Every multi-byte field is big-endian, so load it byte by byte
        ;     and recombine. A single 16-bit load gives you the bytes
        ;     reversed.
        ;   * The fragment offset straddles a byte boundary. Its top five
        ;     bits live in byte 6 and its bottom eight in byte 7. Combine
        ;     both bytes into one word first, then shift and mask.
        ;   * The flags are the top three bits of the same word.
        ;   * Read and store the checksum field like any other field.
        ;     ip_checksum computes the VALID line separately.
        ;   * src and dst are four single-byte stores each. No shifting.
        ;
        ; Nothing here reads the file or prints. This routine only fills
        ; the struct, and driver.c does the rest.
        ;

; Get header and output addresses
        mov     esi, [ebp+8]     ; get the hdr
        mov     edi, [ebp+12]    ; get the out

        ; Version and IHL
        movzx   eax, byte [esi]  ; gets the first byte
        mov     edx, eax         ; copies the first byte

        and     edx, 0x0F        ; keep low 4 bits for IHL
        mov     [edi+4], edx     ; saves IHL

        shr     eax, 4           ; shifts right to get the high 4 bits
        and     eax, 0x0F        ; keep only 4 bits
        mov     [edi+0], eax     ; saves version

        ; DSCP and ECN
        movzx   eax, byte [esi+1]  ; gets the second byte
        mov     edx, eax           ; copies the second byte

        shr     eax, 2             ; shifts right to get the upper 6 bits
        and     eax, 0x3F          ; keeps only 6 bits for DSCP
        mov     [edi+8], eax       ; saves DSCP

        and     edx, 0x03          ; keeps the lower 2 bits for ECN
        mov     [edi+12], edx      ; saves ECN

        ; Total Length
        movzx   eax, byte [esi+2]  ; gets the third byte
        shl     eax, 8             ; shifts left by 8 bits

        movzx   edx, byte [esi+3]  ; gets the fourth byte
        or      eax, edx           ; combines both bytes
        mov     [edi+16], eax      ; saves total length

        ; Identification
        movzx   eax, byte [esi+4]  ; gets the fifth byte
        shl     eax, 8             ; shifts left by 8 bits

        movzx   edx, byte [esi+5]  ; gets the sixth byte
        or      eax, edx           ; combines both bytes
        mov     [edi+20], eax      ; saves identification

        ; Flags and Fragment Offset
        movzx   eax, byte [esi+6]  ; gets the seventh byte
        shl     eax, 8             ; shifts left by 8 bits

        movzx   edx, byte [esi+7]  ; gets the eighth byte
        or      eax, edx           ; combines both bytes

        mov     edx, eax           ; copies the combined bytes
        shr     edx, 13            ; shifts right to get the upper 3 bits
        and     edx, 0x07          ; keeps only 3 bits for flags
        mov     [edi+24], edx      ; saves flags

        and     eax, 0x1FFF        ; keeps the lower 13 bits
        mov     [edi+28], eax      ; saves fragment offset

        ; TTL and Protocol
        movzx   eax, byte [esi+8]  ; gets the ninth byte
        mov     [edi+32], eax      ; saves TTL

        movzx   eax, byte [esi+9]  ; gets the tenth byte
        mov     [edi+36], eax      ; saves protocol

        ; Header Checksum
        movzx   eax, byte [esi+10]  ; gets the eleventh byte
        shl     eax, 8              ; shifts left by 8 bits

        movzx   edx, byte [esi+11]  ; gets the twelfth byte
        or      eax, edx            ; combines both bytes
        mov     [edi+40], eax       ; saves header checksum

        ; Source IP Address
        mov     al, [esi+12]     ; gets the thirteenth byte
        mov     [edi+44], al     ; saves first source IP byte

        mov     al, [esi+13]     ; gets the fourteenth byte
        mov     [edi+45], al     ; saves second source IP byte

        mov     al, [esi+14]     ; gets the fifteenth byte
        mov     [edi+46], al     ; saves third source IP byte

        mov     al, [esi+15]     ; gets the sixteenth byte
        mov     [edi+47], al     ; saves fourth source IP byte

        ; Destination IP Address
        mov     al, [esi+16]     ; gets the seventeenth byte
        mov     [edi+48], al     ; saves first destination IP byte

        mov     al, [esi+17]     ; gets the eighteenth byte
        mov     [edi+49], al     ; saves second destination IP byte

        mov     al, [esi+18]     ; gets the nineteenth byte
        mov     [edi+50], al     ; saves third destination IP byte

        mov     al, [esi+19]     ; gets the twentieth byte
        mov     [edi+51], al     ; saves fourth destination IP byte

        ; Restore registers and return
        popa
        mov     eax, 0
        leave
        ret
